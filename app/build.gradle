apply plugin: 'com.android.application'

apply plugin: 'kotlin-android'

apply plugin: 'kotlin-android-extensions'

apply plugin: 'kotlin-kapt'

apply plugin: "org.sonarqube"

apply plugin: 'jacoco'

androidExtensions {
    experimental = true
}

android {
    def globalConfiguration = rootProject.extensions.getByName("ext")

    compileSdkVersion globalConfiguration["versionAndroidCompileSdk"]

    defaultConfig {
        applicationId "com.flightmode.app"
        minSdkVersion globalConfiguration["versionAndroidMinSdk"]
        targetSdkVersion globalConfiguration["versionAndroidTargetSdk"]
        versionCode globalConfiguration["versionCode"]
        versionName globalConfiguration["versionName"]
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }
    buildTypes {
        debug {
            testCoverageEnabled true
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    testOptions {
        unitTests.all {
            jacoco {
                includeNoLocationClasses = true
            }
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
    }
}

task jacocoTestReport(type: JacocoReport, dependsOn: ['testDebugUnitTest', 'createDebugCoverageReport']) {

    reports {
        xml.enabled = true
        html.enabled = true
    }

    def fileFilter = ['**/R.class', '**/R$*.class', '**/BuildConfig.*', '**/Manifest*.*', '**/*Test*.*', 'android/**/*.*']
    def debugTree = fileTree(dir: "${buildDir}/intermediates/classes/debug", excludes: fileFilter)
    def mainSrc = "${project.projectDir}/src/main/java"

    sourceDirectories = files([mainSrc])
    classDirectories = files([debugTree])
    executionData = fileTree(dir: "$buildDir", includes: [
            "jacoco/testDebugUnitTest.exec",
            "outputs/code-coverage/connected/*coverage.ec"
    ])
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$kotlin_version"

    implementation              androidDependencies.appcompat
    implementation              androidDependencies.constraintlayout
    implementation              androidDependencies.coreKtx
    implementation              androidDependencies.legacySupport
    implementation              androidDependencies.lifecycleExtensions
    kapt                        androidDependencies.lifecycleCompiler
    implementation              androidDependencies.roomRuntime
    kapt                        androidDependencies.roomCompiler

    implementation              thirdPartyDependencies.playServicesMaps
    implementation              thirdPartyDependencies.material
    implementation              thirdPartyDependencies.retrofit
    implementation              thirdPartyDependencies.retrofitConverterGson

    testImplementation          testDependencies.junit
    testImplementation          testDependencies.mockito
    testImplementation          testDependencies.retrofitMock
    testImplementation          testDependencies.robolectric
    androidTestImplementation   testDependencies.testRunner
    androidTestImplementation   testDependencies.espressoCore
}